-- CURSOR (커서)
-- SELECT된 행들을 순차적으로 가리키기 위해 사용하는 포인터(지칭자)

-- 한 행의 데이터를 가리키는 커서
DECLARE 
	-- 커서 선언
	CURSOR CUR_DEPT IS 
		SELECT * FROM DEPARTMENTS WHERE DEPARTMENT_ID = 50;
	-- 커서가 인출한 행의 데이터를 저장할 변수
	V_DEPT_ROW DEPARTMENTS%ROWTYPE;
BEGIN
	-- 커서 오픈
	OPEN CUR_DEPT;
	-- 커서에서 한 행을 인출하여 V_DEPT_ROW에 저장
	FETCH CUR_DEPT INTO V_DEPT_ROW;
	P('부서아이디:'||V_DEPT_ROW.DEPARTMENT_ID);
	P('부서명:'||V_DEPT_ROW.DEPARTMENT_NAME);
	-- 커서 닫기
	CLOSE CUR_DEPT;
END;

-- 여러 행을 가리키는 커서
DECLARE 
	-- 부서테이블의 전체 행을 가리키는 커서 선언
	CURSOR CUR_DEPT IS
		SELECT * FROM DEPARTMENTS;
	-- 각 행의 데이터를 저장할 변수
	V_DEPT_ROW DEPARTMENTS%ROWTYPE;
BEGIN
	OPEN CUR_DEPT;
	LOOP
		FETCH CUR_DEPT INTO V_DEPT_ROW;
		-- 더이상 커서가 가리키는 행이 없다면 반복문을 빠져나감
		EXIT WHEN CUR_DEPT%NOTFOUND;
		P('부서아이디:'||V_DEPT_ROW.DEPARTMENT_ID);
		P('부서명:'||V_DEPT_ROW.DEPARTMENT_NAME);
	END LOOP;
	CLOSE CUR_DEPT;
END;

-- FOR ~ IN 구문과 함께 커서 사용
-- OPEN, FETCH, CLOSE를 자동으로 해주므로 편함
DECLARE
	CURSOR CUR_DEPT IS 
		SELECT * FROM DEPARTMENTS;
	V_DEPT_ROW DEPARTMENTS%ROWTYPE;
BEGIN
	FOR V_DEPT_ROW IN CUR_DEPT
	LOOP
		P('부서아이디:'||V_DEPT_ROW.DEPARTMENT_ID);
		P('부서명:'||V_DEPT_ROW.DEPARTMENT_NAME);
	END LOOP;
END;

-- 파라미터를 사용하는 커서
DECLARE
	CURSOR CUR_DEPT(P_DEPT_ID DEPARTMENTS.DEPARTMENT_ID%TYPE)
	IS
	SELECT * FROM DEPARTMENTS
	WHERE DEPARTMENT_ID = P_DEPT_ID;
BEGIN
	FOR V_DEPT_ROW IN CUR_DEPT(30)
	LOOP
		P('부서아이디:'||V_DEPT_ROW.DEPARTMENT_ID);
		P('부서명:'||V_DEPT_ROW.DEPARTMENT_NAME);
	END LOOP;
END;

